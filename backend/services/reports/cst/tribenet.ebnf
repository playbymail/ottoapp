(* TribeNet Turn Report Grammar - EBNF                                          *)
(* This grammar documents the structure of TribeNet turn reports.               *)
(* It is intended as documentation for implementing a recursive descent parser. *)
(* Terminals start with an upper-case letter and are defined in ../lexers pkg.  *)
(* Spaces are significant in the grammar when called out with the SP terminal.  *)
(* The "_" glyph represents a "run up to" terminal.                             *)

(* ============================================================================ *)
(* Top-Level Structure *)
(* ============================================================================ *)

(* A cleaned turn report contains one or more unit sections *)
turn_report     = unit_section, { unit_section }, EOF ;

unit_section    = unit_line,
                  turn_line,
                  [ unit_movement_line ]
                  { scout_movement_line };

(* ============================================================================ *)
(* Unit Line *)
(* ============================================================================ *)

(* Example: Tribe 0987, , Current Hex = QQ 1315, (Previous Hex = N/A) *)
(* Example: Element 0987e1, , Current Hex = QQ 0303, (Previous Hex = ## 0203) *)
(* Example: Fleet 0987f9, , Current Hex = QQ 0303, (Previous Hex = QQ 1315) *)
unit_line       = unit_keyword, unit_id, Comma, [ Note ], Comma,
                  Current, Hex, Equals, coords, Comma,
                  LeftParen, Previous, Hex, Equals, coords, RightParen, EOL ;

unit_keyword    = Courier | Element | Fleet | Garrison | Tribe ;

unit_id = Number | UnitId ;

coords          = grid_coords | na_coords | obscured_coords ;
grid_coords     = Grid, Number ;                           (* e.g., QQ 1315 *)
na_coords       = Text, Slash, Text ;                      (* N/A, not available *)
obscured_coords = Hash, Hash, Number ;                     (* ## 1315, obscured location *)

(* ============================================================================ *)
(* Turn Line *)
(* We expect (but do not require) the Clan section to have the full turn info.  *)
(* ============================================================================ *)

(* Example: Current Turn 899-12 (#0), Winter, FINE Next Turn 900-01 (#1), 28/11/2025 *)
(* Example: Current Turn 900-01 (#1), Spring, FINE *)
turn_line       = Current, Turn, TurnYearMonth, turn_number, Comma, Season, Comma, Weather,
                  [ Next, Turn, TurnYearMonth, turn_number, Comma, report_date ],
                  EOL ;

turn_number     = LeftParen, Hash, Number, RightParen ;     (* e.g., (#0), (#1) *)
report_date     = Number, Slash, Number, Slash, Number ;    (* DD/MM/YYYY *)

(* ============================================================================ *)
(* Unit Movement *)
(* ============================================================================ *)

unit_movement_line = unit_goes_to_line |
                     land_movement_line ;

(* Example: Tribe Goes to QQ 1612 *)
unit_goes_to_line = Tribe, Goes, To, grid_coords ;

(* Example: Tribe Movement: Move S-PR, *)
land_movement_line = Tribe, Movement, Colon, land_movement, EOL ;

(* ============================================================================ *)
(* Land Movement *)
(* ============================================================================ *)

(* Land Movement is a backslash-separated sequence of steps. *)
(* Steps may be empty or full. *)
(* An empty step is an artifact of the report generator but must be parsed *)
(*   Example (empty step): Move *)
(*   Example (empty step): \ *)
(* A full step describes entering a hex: Direction Dash Terrain Comma *)
(*   Example (full step): Move NE-PR, *)
(*   Example (full step): \NE-PR, *)
(*   Example (full step, full step): Move NE-PR, \NE-PR, *)
(* Empty steps can appear anywhere in a movement line *)
(*   Example (full step, empty step): Move NE-PR, \ *)
(*   Example (full step, empty step, empty step): Move SE-PR, \\ *)
(*   Example (empty step, full step, empty step): Move \SE-PR, \ *)
land_movement  = Move, land_step, { Backslash , land_step } ;

(* Land step movement is optional; result is required for a full step *)
land_step = [ [ land_step_movement ], land_step_result ];

land_step_movement = Direction, Dash, Terrain ;

(* Grammar for results will be enhanced as we parse more complex turn results *)
land_step_result = Comma, ( visible_neighbor | unit_exhausted, [ Comma, found_nothing ] );

(* Visible neighbor is terrain followed by list comma separated directions *)
(* Example: L NE, SW *)
visible_neighbor = Terrain, Direction, { Comma, Direction } ;

(* Visible unit is a list of space separated units *)
(* Example: 0987 1987 *)
(* Bugs: the list is not always preceded by a Comma *)
(*   Example: 0987e1 Status: CONIFER HILLS, L NE, SE, N 0987e1 *)
(*   Example: 1987 Status: LOW CONIFER MOUNTAINS, Lcm SW, NW, N, 1987 *)
visible_unit = UnitId, { UnitId };

empty_result = Comma ;

(* ============================================================================ *)
(* Scout Movement *)
(* ============================================================================ *)

scout_movement_line = Scout, ScoutId, Colon, Scout  ;

scout_movement = Scout, scout_step, { Backslash, scout_step };

scout_step = [ scout_step_movement, scout_step_result ];

scout_step_movement = Direction, Dash, Terrain ;

(* Grammar for results will be enhanced as we parse more complex turn results *)
scout_step_result = Comma ;

unit_exhausted = Not, Enough, MPs, To, Move, To, Direction, Into, Terrain ;
found_nothing = Nothing, Of, Interest, Found ;